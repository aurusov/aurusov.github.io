$Pattern Образец_Обслуживание_клиента : operation 
$Relevant_resources
	_Клиент  : Клиенты  keep keep
	_Продавец: Продавцы keep keep
	with_min _Клиент.Время_прихода
$Time = Равномерный_закон (18.12,34.35)
$Body
_Клиент
	Choice from _Клиент.Состояние == В_очереди
	Convert_begin
		Состояние = Покупает;
	Convert_end
		Состояние      = Купил;
		Время_ожидания = time_now - _Клиент.Время_прихода;

_Продавец
	Choice from _Продавец.Занятость_продавца == Свободен and
	            _Продавец.Расположение == _Клиент.Направление
	Convert_begin
		Занятость_продавца = Обслуживает;
	Convert_end
		Занятость_продавца = Свободен;
$End

$Pattern Образец_Прием_пищи : operation
$Relevant_resources
	_Клиент  : Клиенты  keep erase
	_Пищеблок: Пищеблок keep keep
	first
$Time = Время_приема_пищи ()
$Body
_Клиент
	Choice from _Клиент.Состояние == Купил
	Convert_begin
		Состояние  = Ест;
		Размещение = Размещение_по_гистограмме;
	Convert_end
		Состояние  = Поел;

_Пищеблок 
	Choice from _Клиент.Состояние == Купил
	Convert_end
	Количество_ушедших  = _Пищеблок.Количество_ушедших + 1;
$End
