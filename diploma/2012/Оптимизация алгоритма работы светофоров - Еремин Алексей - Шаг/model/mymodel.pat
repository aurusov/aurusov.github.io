$Pattern Образец_Разгон: operation trace
$Relevant_resources
	_машина  : Машины Keep Keep
$Time = Шаг_интегрирования
$Body
_машина
	Choice from
		_машина.Состояние == РазгонВозможен and
		_машина.Проверка  == Окончена
	Convert_begin
		Состояние = Разгон;
		Проверка  = Начать;
	Convert_end
		Скорость    = Скорость_разгона(_машина.Скорость); 
		XКоордината = Координата_разгона_X(_машина.XКоордината, _машина.Скорость, _машина.Направление);
		YКоордината = Координата_разгона_Y(_машина.YКоордината, _машина.Скорость, _машина.Направление);
		Состояние   = Проверка;
$End

$Pattern Образец_Движение: operation trace
$Relevant_resources
	_машина  : Машины    Keep     Keep
$Time = Шаг_интегрирования
$Body
_машина
	Choice from
		_машина.Состояние == ЕздаВозможна and
		_машина.Проверка  == Окончена
	Convert_begin
		Состояние = Езда;
		Проверка  = Начать;
	Convert_end
		XКоордината = Координата_езды_X(_машина.XКоордината, _машина.Направление);
		YКоордината = Координата_езды_Y(_машина.YКоордината, _машина.Направление);
		Состояние   = Проверка;
$End

$Pattern Образец_Торможение: operation trace
$Relevant_resources
	_машина   : Машины    Keep     Keep
$Time = Шаг_интегрирования
$Body
_машина	
	Choice from
		_машина.Состояние == ТорможениеВозможно and
		_машина.Проверка  == Окончена
	Convert_begin
		Состояние = Торможение;
		Проверка  = Начать;
	Convert_end
		Скорость    = Скорость_торможения(_машина.Скорость);
		XКоордината = Координата_торможения_X(_машина.XКоордината, _машина.Скорость, _машина.Направление);
		YКоордината = Координата_торможения_Y(_машина.YКоордината, _машина.Скорость, _машина.Направление);
		Состояние   = Проверка;
$End

$Pattern Образец_Проверка_препятствия: rule trace
$Relevant_resources
	_машина  : Машины    Keep     
	_светофор: Светофоры NoChange 
		with_min Минимальная_координата_1(_машина.Направление, _светофор.XКоордината, _светофор.YКоордината)
$Body
_машина
	Choice from
		_машина.Состояние == Проверка and
		_машина.Проверка  == Начать
	Convert_rule
		Препятствие = Препятствие(_машина.XКоордината, _машина.YКоордината, _светофор.XКоордината, _светофор.YКоордината, _машина.Скорость, _светофор.Сигнал, _машина.Направление);
		Состояние   = Состояние_1(_машина.Препятствие, _машина.XКоордината, _машина.YКоордината, _светофор.XКоордината, _светофор.YКоордината, _машина.Скорость, _светофор.Сигнал, _машина.Направление);
		Проверка    = Окончена;
_светофор
	Choice from
		(_светофор.Направление == _машина.Направление and
		_светофор.Направление == 1                   and
		_светофор.XКоордината  > _машина.XКоордината)        or
		(_светофор.Направление == _машина.Направление and
		_светофор.Направление == 2                   and
		_светофор.YКоордината  > _машина.YКоордината)        or
		(_светофор.Направление == _машина.Направление and
		_светофор.Направление == 4                   and
		_светофор.YКоордината  < _машина.YКоордината)         or
		(_машина.Направление == 1        and // т.е. машина на горизонтальном участке "полукольца". это нужно чтобы она вовремя затормозила перед поворотом "вверх"
		_машина.YКоордината >= 607.0)                         or
		(_машина.Направление == 2 and
		_машина.YКоордината >= 300.0) // на подходе к повороту
$End

$Pattern Образец_Проверка_препятствия_3: rule trace
$Relevant_resources
	_машина      : Машины    Keep     
	_светофор    : Светофоры NoChange 
	_препятствие : Машины    NoChange 
	with_min Минимальная_координата_3(_машина.Направление, _светофор.XКоордината, _светофор.YКоордината, _препятствие.XКоордината, _препятствие.YКоордината)
$Body
_машина
	Choice from
		_машина.Состояние == Проверка and
		_машина.Проверка  == Начать
	Convert_rule
		Препятствие = Препятствие_3(_машина.Направление, _машина.XКоордината, _машина.YКоордината, _препятствие.XКоордината, _препятствие.YКоордината, _светофор.XКоордината, _светофор.YКоордината, _машина.Скорость, _препятствие.Скорость, _препятствие.Препятствие, _светофор.Сигнал, _машина.Куда, _машина.Откуда, _препятствие.Направление);
		Состояние   = Состояние_3(_машина.Препятствие, _машина.Направление, _машина.XКоордината, _машина.YКоордината, _препятствие.XКоордината, _препятствие.YКоордината, _светофор.XКоордината, _светофор.YКоордината, _машина.Скорость, _препятствие.Скорость, _препятствие.Препятствие, _светофор.Сигнал, _машина.Куда, _машина.Откуда);
		Проверка    = Окончена;
_светофор
	Choice from
		_светофор.Направление == _машина.Направление and
		((_светофор.Направление == 1 and _светофор.XКоордината  > _машина.XКоордината and _светофор.YКоордината == 425) or
		 (_светофор.Направление == 2 and _светофор.YКоордината  > _машина.YКоордината and _светофор.XКоордината == _машина.XКоордината) or
		 (_светофор.Направление == 4 and _светофор.YКоордината  < _машина.YКоордината and _светофор.XКоордината == _машина.XКоордината))
_препятствие
	Choice from
		_препятствие.Состояние   <> Подг_Удаление       and
		(_препятствие.Направление == _машина.Направление and
		((_препятствие.Направление == 1 and _препятствие.XКоордината  > _машина.XКоордината and _препятствие.YКоордината == _машина.YКоордината)   or
		 (_препятствие.Направление == 2 and _препятствие.YКоордината  > _машина.YКоордината and _препятствие.XКоордината == _машина.XКоордината)   or
		 (_препятствие.Направление == 4 and _препятствие.YКоордината  < _машина.YКоордината and _препятствие.XКоордината == _машина.XКоордината))  or
		 (_препятствие.Направление == 2 and _машина.Направление == 1 and _машина.XКоордината < 500 and Select(Машины: abs(Машины.YКоордината - 355) < Запас / 2 and Машины.Направление == 2).Size() > 0 ))
		 
$End

$Pattern Образец_Проверка_препятствия_2: rule trace
$Relevant_resources
	_машина      : Машины    Keep     
	_препятствие : Машины    NoChange 
	with_min Минимальная_координата_2(_машина.Направление,_препятствие.XКоордината, _препятствие.YКоордината)
$Body
_машина
	Choice from
		_машина.Состояние == Проверка and
		_машина.Проверка  == Начать
	Convert_rule
		Препятствие = Препятствие_2(_машина.XКоордината, _машина.YКоордината, _препятствие.XКоордината, _препятствие.YКоордината, _машина.Скорость, _препятствие.Скорость, _препятствие.Препятствие, _машина.Направление);
		Состояние   = Состояние_2(_машина.Препятствие, _машина.XКоордината, _машина.YКоордината, _препятствие.XКоордината, _препятствие.YКоордината, _машина.Скорость, _препятствие.Скорость, _препятствие.Препятствие, _машина.Направление);
		Проверка    = Окончена;
_препятствие
	Choice from
		_препятствие.Состояние   <> Подг_Удаление            and
		_препятствие.Направление == _машина.Направление and
		((_препятствие.Направление == 1 and _препятствие.XКоордината  > _машина.XКоордината and _препятствие.YКоордината == _машина.YКоордината) or
		 (_препятствие.Направление == 2 and _препятствие.YКоордината  > _машина.YКоордината and _препятствие.XКоордината == _машина.XКоордината) or
		 (_препятствие.Направление == 4 and _препятствие.YКоордината  < _машина.YКоордината and _препятствие.XКоордината == _машина.XКоордината))
$End

$Pattern Образец_Переключение_светофора: operation trace
$Relevant_resources
	_светофор: Светофоры Keep Keep
$Time = Время_переключения_светофора(_светофор.Сигнал, _светофор.Направление)
$Body
_светофор
	Choice from
		_светофор.Переключение == false
	Convert_begin
		Переключение = true;
	Convert_end
		Сигнал       = Новый_сигнал(_светофор.Сигнал);
		Переключение = false;
$End
 
$Pattern Образец_Нет_препятствия: rule trace
$Relevant_resources
	_машина: Машины Keep
$Body
_машина
	Choice from
		_машина.Состояние == Проверка and
		_машина.Проверка  == Начать
	Convert_rule
		Состояние   = Состояние_нет_препятствия(_машина.Скорость);
		Проверка    = Окончена;
		Препятствие = 0;
$End

$Pattern Образец_Появление_машины_шоссе: rule trace 
$Relevant_resources
	_другая_машина: Машины NoChange
	_машина       : Машины Create
		with_min _другая_машина.XКоордината
$Body
_другая_машина
	Choice from
		_другая_машина.Состояние <> Подг_Удаление and
		_другая_машина.Направление == 1     and
		Select(Машины: Машины.XКоордината < Запас).Empty()
_машина
	Convert_rule trace
		XКоордината   = 0.333;
		YКоордината   = 355.0;
		Скорость      = 0.0;
		Состояние     = Проверка;
		Препятствие   = 0;
		Направление   = 1;
		Проверка      = Начать;
		Цвет          = 65280;
		Куда          = Вправо;
		Откуда        = Шоссе;
		Время_приезда = Time_now;
		Время_уезда   = Time_now; // значение этого параметра не важно на данном этапе функционирования машины
$End

$Pattern Образец_Появление_машины_улица: rule trace 
$Relevant_resources
	_другая_машина: Машины NoChange
	_машина       : Машины Create
		with_min _другая_машина.YКоордината
$Body
_другая_машина
	Choice from
		_другая_машина.Состояние <> Подг_Удаление and
		_другая_машина.Направление == 2           and
		Select(Машины: Машины.YКоордината < Запас).Empty()
_машина
	Convert_rule trace
		XКоордината = 500.0;
		YКоордината = 0.0;
		Скорость    = 0.0;
		Состояние   = Проверка;
		Препятствие = 0;
		Направление = 2;
		Проверка    = Начать;
		Цвет        = 65280;
		Куда        = Розыгрыш_куда_2;
		Откуда      = Улица;
		Время_приезда = Time_now;
		Время_уезда   = Time_now; // значение этого параметра не важно на данном этапе функционирования машины
$End

$Pattern Образец_Подготовка_удаления_машины: operation trace
$Relevant_resources
	_машина: Машины Keep Keep
$Time = Шаг_интегрирования // чтобы все процессы, начавшиеся при помощи этой машины были успешно завершены
$Body
_машина
	Choice from
		(_машина.Состояние <> Подг_Удаление) and
		(
			(_машина.XКоордината >= 1100 and _машина.Направление == 1) or
			(_машина.XКоордината <= 0    and _машина.Направление == 3)
		)
	Convert_begin
		Состояние = Подг_Удаление;
	Convert_end
		Состояние = Удаление;
$End	

$Pattern Образец_Удаление: rule trace
$Relevant_resources
	_машина: Машины Erase
$Body
_машина
	Choice from
		_машина.Состояние == Удаление
	Convert_rule
		Время_уезда = Time_now;
$End

$Pattern Образец_Поворот_влево: rule trace
$Relevant_resources
	_машина: Машины Keep
$Body
_машина
	Choice from
		_машина.Откуда      == Улица and
		_машина.Куда        == Влево and
		_машина.Направление == 2     and
		_машина.YКоордината >= 300
	Convert_rule
		Направление = 3 ;
$End

$Pattern Образец_Поворот_вправо: rule trace
$Relevant_resources
	_машина: Машины Keep
$Body
_машина
	Choice from
		_машина.Откуда      == Улица  and
		_машина.Куда        == Вправо and
		_машина.Направление == 2      and
		_машина.YКоордината >= 607    and
		Select(Машины: Машины.Направление == 1 and Машины.YКоордината > 600.0 and Машины.XКоордината - 500 < Запас).Empty()// никакая машина не мешает повороту
	Convert_rule
		Направление = 1  ;
		YКоордината = 607.0;
$End

$Pattern Образец_Поворот_вверх: rule trace
$Relevant_resources
	_машина       : Машины Keep
$Body
_машина
	Choice from
		_машина.Откуда      == Улица  and
		_машина.Куда        == Вправо and
		_машина.Направление == 1      and
		_машина.XКоордината >= 946    and
		_машина.YКоордината >= 600    and 
		Select(Машины: Машины.Направление == 4 and abs(Машины.YКоордината - 607) < Запас).Empty()// никакая машина не мешает повороту
	Convert_rule
		Направление = 4  ;
		XКоордината = 946.0;
$End

$Pattern Образец_Поворот_вправо_2: rule trace
$Relevant_resources
	_машина: Машины Keep
$Body
_машина
	Choice from
		_машина.Откуда      == Улица  and
		_машина.Куда        == Вправо and
		_машина.Направление == 4      and
		_машина.YКоордината <= 355
	Convert_rule
		Направление = 1  ;
		YКоордината = 355.0;
$End

