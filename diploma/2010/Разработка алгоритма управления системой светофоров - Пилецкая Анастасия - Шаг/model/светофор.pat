// ------------------------------------------------------------------
// ----- Модель приезда машин
// ------------------------------------------------------------------
$Pattern Образец_Прибытие_на_главную_дорогу: irregular_event
$Relevant_resources
	Новое_транспортное_средство: Транспортные_средства create
$Time = Интервал_приезда_машин(1.42 / 60)
$Body
Новое_транспортное_средство
	Convert_event trace
		Тип            set Розыгрыш_типа_транспортного_средства
		Состояние      set приезжает
		Время_прибытия set Time_now
		Номер_полосы   set Розыгрыш_полосы_главной_дороги
		Положение      set -100
$End

$Pattern Образец_Прибытие_на_второстепенную_дорогу: irregular_event
$Relevant_resources
	Новое_транспортное_средство: Транспортные_средства create
$Time = Интервал_приезда_машин(9.47 / 60)
$Body
Новое_транспортное_средство
	Convert_event trace
		Тип            set Розыгрыш_типа_транспортного_средства
		Состояние      set приезжает
		Время_прибытия set Time_now
		Номер_полосы   set полоса_6
		Положение      set -100
$End

$Pattern Образец_В_конец_полосы: rule
$Relevant_resources
	_ТС    : Транспортные_средства Keep
	_Полоса: Полосы                Keep
	_Дорога: Дороги                Keep
$Body
_ТС
	Choice from _ТС.Состояние = приезжает
	Convert_rule
		Состояние set стоит
		Положение set Положение_новой_машины(_Полоса.Длина_пробки)

_Полоса
	Choice from _Полоса.Номер_полосы = _ТС.Номер_полосы
	Convert_rule
		Количество_ТС   set _Полоса.Количество_ТС + 1
		Длина_пробки 	set Положение_новой_машины(_Полоса.Длина_пробки) + Длина_машины(_ТС.Тип)

_Дорога
	Choice from Дорога(_Полоса.Номер_полосы) == _Дорога.Дорога
	Convert_rule
		Количество_ТС_на_дороге set _Дорога.Количество_ТС_на_дороге + 1
$End

// ------------------------------------------------------------------
// ----- Модель СУ сфетофора
// ------------------------------------------------------------------
$Pattern Образец_Переключение_сигнала_светофора_1_на_зелёный: rule
$Parameters
	Дорога      : such_as Светофоры.Дорога
	Длительность: real
$Relevant_resources
	_Светофор1: Светофоры Keep
$Body
_Светофор1
	Choice from _Светофор1.Дорога == Дорога and _Светофор1.Сигнал == красный and Time_now >= _Светофор1.Время_красного + Длительность
	Convert_rule
		Сигнал 			set зелёный
		Время_зелёного 	set Time_now
$End

$Pattern Образец_Переключение_сигнала_светофора_1_на_красный: rule
$Parameters
	Дорога      : such_as Светофоры.Дорога
	Длительность: real
$Relevant_resources
	_Светофор1: Светофоры Keep
$Body
_Светофор1
	Choice from _Светофор1.Дорога == Дорога and _Светофор1.Сигнал == зелёный and Time_now >= _Светофор1.Время_зелёного + Длительность
	Convert_rule
		Сигнал 			set красный
		Время_красного 	set Time_now
$End

$Pattern Образец_Переключение_сигнала_светофора_2_на_зелёный: rule
$Relevant_resources
	_Светофор1: Светофор_1 NoChange
	_Светофор2: Светофор_2 Keep
$Body
_Светофор1
	Choice from _Светофор1.Сигнал == красный

_Светофор2
	Choice from _Светофор2.Сигнал == красный
	Convert_rule
		Сигнал 			set зелёный
		Время_зелёного 	set Time_now
$End

$Pattern Образец_Переключение_сигнала_светофора_2_на_красный: rule
$Relevant_resources
	_Светофор1: Светофор_1 NoChange
	_Светофор2: Светофор_2 Keep
$Body
_Светофор1
	Choice from _Светофор1.Сигнал == зелёный

_Светофор2
	Choice from _Светофор2.Сигнал == зелёный
	Convert_rule
		Сигнал 			set красный
		Время_красного 	set Time_now
$End

$Pattern Образец_Время_прерёд: irregular_event
$Relevant_resources
	_Светофор: Светофор_1 Keep
$Time = 5.0/60.0
$Body
_Светофор
	Convert_event
		Дорога set _Светофор.Дорога
$End

// ------------------------------------------------------------------
// ----- Модель отъезда машин
// ------------------------------------------------------------------
$Pattern Образец_Отъезда_машин: operation
$Parameters
	Дорога: such_as Дороги.Дорога
$Relevant_resources
	_Светофор: Светофоры             NoChange NoChange
	_ТС      : Транспортные_средства Keep     Keep
	_Полоса  : Полосы                Keep     NoChange
$Time = Длина_машины(_ТС.Тип) / 35 / 60
$Body
_Светофор
	Choice from _Светофор.Дорога == Дорога and _Светофор.Сигнал == зелёный

_ТС
	Choice from _ТС.Состояние == стоит and _ТС.Положение <= 0.0
	Convert_begin
		Состояние set уезжает
	Convert_end
		Состояние set уехала

_Полоса
	Choice from _Полоса.Состояние == стоит and _Полоса.Номер_полосы = _ТС.Номер_полосы and _Полоса.Дорога == Дорога
	Convert_begin
		Состояние               set продвигается
		Количество_ТС           set _Полоса.Количество_ТС - 1
		Длина_уехавшей          set Длина_машины(_ТС.Тип)
		Длина_пробки            set _Полоса.Длина_пробки - _Полоса.Длина_уехавшей - Интервал_между_машинами
$End

$Pattern Образец_Удаления_машины: rule
$Relevant_resources
	_ТС: Транспортные_средства Erase
$Body
_ТС
	Choice from _ТС.Состояние == уехала
$End

$Pattern Образец_Продвижения_машин: rule trace
$Relevant_resources
	_Полоса: Полосы                NoChange
	_ТС    : Транспортные_средства Keep
$Body
_Полоса
	Choice from _Полоса.Состояние == продвигается

_ТС
	Choice from _ТС.Состояние == стоит and _Полоса.Номер_полосы = _ТС.Номер_полосы
	Convert_rule
		Состояние set движется
$End

$Pattern Образец_Остановки_полосы_начало: rule
$Relevant_resources
	_Полоса: Полосы Keep
$Body
_Полоса
	Choice from _Полоса.Состояние == продвигается and
	            Select(Транспортные_средства: Транспортные_средства.Номер_полосы == _Полоса.Номер_полосы).For_All(Транспортные_средства.Состояние == движется)
	Convert_rule
		Состояние set конец_продвижения
$End

$Pattern Образец_Остановки_машин_на_полосе: rule
$Relevant_resources
	_Полоса: Полосы NoChange
	_ТС    : Транспортные_средства Keep
$Body
_Полоса
	Choice from _Полоса.Состояние == конец_продвижения

_ТС
	Choice from _ТС.Состояние == движется and _Полоса.Номер_полосы = _ТС.Номер_полосы
	Convert_rule
		Состояние set стоит
		Положение set _ТС.Положение - _Полоса.Длина_уехавшей - Интервал_между_машинами
$End

$Pattern Образец_Остановки_полосы_конец: rule
$Relevant_resources
	_Полоса: Полосы Keep
$Body
_Полоса
	Choice from _Полоса.Состояние == конец_продвижения and
	            Select(Транспортные_средства: Транспортные_средства.Номер_полосы == _Полоса.Номер_полосы).For_All(Транспортные_средства.Состояние == стоит)
	Convert_rule
		Состояние set стоит
$End
