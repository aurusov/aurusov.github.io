$Pattern Образец_Приезда_поезда_на_чертановскую: irregular_event 
$Relevant_resources
	_Поезд : Поезда Create
$Time = Равномерный_интервал_между_приездом_поездов(0.386,1.219){ интервал ммежду приездом поездов}
$Body
_Поезд 
	Convert_event 
		Приезд set time_now
		Temp set 10
		Маршрут set Чертановская
		Отъезд set 0
		Время_в_пути set 0
		Начал_движение set 0
		Закончил_движение set 0
		Состояние set Создание
		Перевозим_пассажиров set Fun(Количество_приходящих_людей(203))
$End

$Pattern Образец_Посадка_на_чертановской: operation 
$Relevant_resources
	_Поезд  : Поезда  Keep Keep
	_Станция: Станции Keep Keep
$Time = Равномерный_интервал_времени_посадки(0.216,0.433)
$Body
_Поезд
	Choice from _Поезд.Temp = 10 and _Поезд.Маршрут = Чертановская
	Convert_begin
		Temp set 111
	Convert_end
		Temp set 0
		Отъезд set time_now
		Время_в_пути set _Поезд.Время_в_пути + _Поезд.Отъезд - _Поезд.Приезд
		Состояние set В_пути

_Станция
	Choice from _Станция.Название == _Поезд.Маршрут
	Convert_begin
		Состояние set ЕстьПоезд
	Convert_end
		Состояние set Пустая
$End

$Pattern Образец_Интервал_Чертановская_Серпуховская: operation //ToDo: Равномерный интервал сделать больше данных
$Relevant_resources
	_Поезд  : Поезда   Keep Keep
	_Перегон: Перегоны Keep NoChange
$Time = Время_в_перегоне_Чертановская_Серпуховская(14.233, 14.716)
$Body
_Поезд
	Choice from _Поезд.Temp = 0 and _Поезд.Маршрут = Чертановская
	Convert_begin
		Начал_движение set time_now
		Temp set 1
	Convert_end
		Закончил_движение set time_now
		Temp set 2
		Время_в_пути set _Поезд.Время_в_пути + _Поезд.Закончил_движение - _Поезд.Начал_движение
		
_Перегон
	Choice from _Перегон.От == _Поезд.Маршрут
	Convert_begin
		Количество_поездов set _Перегон.Количество_поездов + 1
$End

$Pattern Образец_Высадка_поезда_на_Серпуховской: operation 
$Relevant_resources
	_Поезд  : Поезда   Keep Keep
	_Очередь: Очередь  Keep NoChange
	_Перегон: Перегоны Keep NoChange
$Time = Время_высадки_людей_на_Серпуховской(0.166,0.25) // от 10 до 15 секунд
$Body
_Поезд
	Choice from _Поезд.Temp = 2 and _Поезд.Маршрут = Чертановская
	Convert_begin
		Temp                 set 3

	Convert_end
		Temp                 set 4
		Время_в_пути         set _Поезд.Время_в_пути + Time_now - _Поезд.Закончил_движение 
		Состояние            set Удаление
		Перевозим_пассажиров set 0

_Очередь
	Convert_begin 
		Возможность_принять_пассажиров set Да
		Идёт_в_очередь                 set _Поезд.Перевозим_пассажиров

_Перегон
	Choice from _Перегон.От == _Поезд.Маршрут
	Convert_begin
		Количество_поездов set _Перегон.Количество_поездов - 1
$End

$Pattern Образец_Удалние_поезда_на_первом_участке: rule 
$Relevant_resources
	_Поезд    : Поезда   Erase
$Body
_Поезд
	Choice from _Поезд.Состояние = Удаление and _Поезд.Маршрут = Чертановская
$End

////////

$Pattern Образец_Пополнение_очереди: operation 
$Relevant_resources
	_Очередь : Очередь Keep Keep
$Time = 0.166
$Body
_Очередь
	Choice from _Очередь.Возможность_принять_пассажиров = Да and _Очередь.Идёт_в_очередь > 0
	Convert_begin
		Возможность_принять_пассажиров set В_процессе
	Convert_end
		Возможность_принять_пассажиров set Нет
		Длинна                         set _Очередь.Длинна + _Очередь.Идёт_в_очередь
		Идёт_в_очередь                 set 0
$End


$Pattern Образец_Уменьшение_очереди_событие: irregular_event 
$Relevant_resources
	_Эскалатор_1	: Эскалатор_1	Keep
	_Эскалатор_2	: Эскалатор_2	Keep
	_Эскалатор_3	: Эскалатор_3	Keep
$Time = 0.016 //Время приезда 2 ступенек 1 секунда
$Body
_Эскалатор_1
	Convert_event
		Возможность_принять_пассажиров	set Да
_Эскалатор_2
	Convert_event
		Возможность_принять_пассажиров	set Да
_Эскалатор_3
	Convert_event
		Возможность_принять_пассажиров	set Да
$End

$Pattern Образец_Уменьшение_очереди_правило: rule 
$Relevant_resources
	_Эскалатор	: Эскалаторы	Keep
	_Очередь 	: Очередь		Keep
$Body
_Эскалатор
	Choice from _Эскалатор.Возможность_принять_пассажиров = Да
	Convert_rule
		Возможность_принять_пассажиров	set Нет
_Очередь
	Choice from _Очередь.Длинна > 0
	Convert_rule
		Длинна set _Очередь.Длинна - 1
$End

///////////
$Pattern Образец_Приезда_поезда_на_добрынинскую: irregular_event 
$Relevant_resources
	_Поезд : Поезда Create
$Time = Равномерный_интервал_между_приездом_поездов(0.386,1.219){ интервал ммежду приездом поездов}
$Body
_Поезд 
	Convert_event 
		Приезд set time_now
		Temp set 10
		Маршрут set Добрынинская
		Отъезд set 0
		Время_в_пути set 0
		Начал_движение set 0
		Закончил_движение set 0
		Состояние set Создание
		Перевозим_пассажиров set Fun(Количество_приходящих_людей(203))
$End

$Pattern Образец_Посадка_на_добрынинской: operation 
$Relevant_resources
	_Поезд : Поезда Keep Keep
	_Станция: Станции Keep Keep
$Time = Равномерный_интервал_времени_посадки(0.216,0.433)
$Body
_Поезд
	Choice from _Поезд.Temp = 10 and _Поезд.Маршрут = Добрынинская
	Convert_begin
		Temp set 111
	Convert_end
		Temp set 0
		Отъезд set time_now
		Время_в_пути set _Поезд.Время_в_пути + _Поезд.Отъезд - _Поезд.Приезд
		Состояние set В_пути
		
_Станция
	Choice from _Станция.Название == _Поезд.Маршрут
	Convert_begin
		Состояние set ЕстьПоезд
	Convert_end
		Состояние set Пустая
$End

$Pattern Образец_Интервал_Добрынинская_Курская: operation 
$Relevant_resources
	_Поезд : Поезда Keep Keep
	_Перегон: Перегоны Keep NoChange
$Time = Время_в_перегоне_Добрынинская_Курская(6.066,6.533) 
$Body
_Поезд
	Choice from _Поезд.Temp = 0 and _Поезд.Маршрут = Добрынинская
	Convert_begin
		Начал_движение set time_now
		Temp set 1
	Convert_end
		Закончил_движение set time_now
		Temp set 2
		Время_в_пути set _Поезд.Время_в_пути + _Поезд.Закончил_движение - _Поезд.Начал_движение
		Состояние set Удаление
		
_Перегон
	Choice from _Перегон.От == _Поезд.Маршрут
	Convert_begin
		Количество_поездов set _Перегон.Количество_поездов + 1
$End

$Pattern Образец_Удалние_поезда_на_втором_участке: rule 
$Relevant_resources
	_Поезд : Поезда Erase
	_Перегон: Перегоны Keep
$Body
_Поезд
	Choice from _Поезд.Состояние = Удаление and _Поезд.Маршрут = Добрынинская
	
_Перегон
	Choice from _Перегон.От == _Поезд.Маршрут
	Convert_rule
		Количество_поездов set _Перегон.Количество_поездов - 1
$End

